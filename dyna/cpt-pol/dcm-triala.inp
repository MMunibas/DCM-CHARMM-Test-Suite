* solvated trialanine NPT simulation used to test stability of DCM simulations with
* barostat and thermostat and polarizable atoms
*

bomlev -1
prnlev 2 node 0

set dcmfile par/ala-14chgs.dcm

open unit 30 card read name par/top_all36_prot.rtf
read rtf card unit 30
close unit 30

open unit 31 card read name par/par_all36_prot.prm
read para card flex unit 31
close unit 31

stream par/toppar_water_ions.str

open unit 10 card read name par/dcm-equi.psf
read psf card unit 10
close unit 10

open unit 20 card read name par/dcm-equi.crd
read coor card unit 20
close unit 20

! pbc

crystal defi cubic 28.65 28.65 28.65 90. 90. 90.
crystal build nope 0
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele .not. segi pep end
image byseg xcen 0.0 ycen 0.0 zcen 0.0 sele segi pep end

!set nonbonding parameters

nbond nbxmod 5 atom cdie eps 1.0 -
  cutnb 14.0 cutim 14.0 ctofnb 12.0 ctonnb 10.0 -
  vswitch vatom vdist shift e14f 1.0

update

! set DCM charges

open unit 10 card read name @dcmfile
DCM IUDCM 10 TSHIFT
close unit 10


! amoeba parameters
xtbd repl mors 141.6662 1.2307 2.1118 sele bynu 11 end sele bynu 12 end
xtbd repl mors 141.6662 1.2307 2.1118 sele bynu 21 end sele bynu 22 end
xtbd repl mors 141.6662 1.2307 2.1118 sele bynu 31 end sele bynu 32 end



!coor stat sele .not. ((segid WAT) .or. (segid CLA)) end
!coor tran xdir -5.0 ydir -12.0 zdir 9.0 sele .not. ((segid WAT) .or. (segid CLA)) end
!coor stat sele .not. ((segid WAT) .or. (segid CLA)) end

!cons fix sele .not. ((segid WAT)) end
shake bonh para tol 1.0e-8

energy

SCAL mass stat
CALC pist = 0.2 * ?stot

OPEN READ  UNIT 30 CARD NAME par/dcm-equi.res   ! Restart file
OPEN WRITE UNIT 31 CARD NAME dcm-cpt.res        ! Restart file
OPEN WRITE UNIT 32 FILE NAME dcm-cpt.dcd        ! Coordinates file

DYNA RESTRT CPT NSTE 25000 TIME 0.001 inbfrq 10 IMGFRQ 10 -
   IPRFRQ 1000 IEQFRQ 500 IHBFRQ 0 NTRFRQ 500 -
   IUNREA 30 IUNWRI 31 IUNCRD 32 IUNVEL -1 -
   NPRINT 100 nsavc 100  -
   pcon pint pmass 500.0 pgam 20.0 pref 1.0 -
   hoover reft 298. tmas @pist


OPEN UNIT 1 WRITE CARD NAME dcm-cpt.pdb
WRITE COOR PDB UNIT 1
CLOSE UNIT 1


stop
